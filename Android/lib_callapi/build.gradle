apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'
apply plugin: 'de.undercouch.download'
apply from: 'rtm.gradle'

android {
    namespace 'io.agora.callapi'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug{
            minifyEnabled false
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

task unzipJar(type: Copy) {
    delete("$buildDir/libs/agora-rtm-sdk-new")
    delete("$buildDir/libs/agora-rtm-sdk.jar")
    from zipTree('third-party/agora-rtm-sdk.jar')
    exclude "io/agora/base/internal/**"
    into ("$buildDir/libs/agora-rtm-sdk-new")
}
task jar(type: Jar, dependsOn: ['unzipJar']){
    baseName 'agora-rtm-sdk'
    from("$buildDir/libs/agora-rtm-sdk-new")
}
task copyJarFromlib(type: Copy, dependsOn:["jar"]){
    from files("$buildDir/libs/agora-rtm-sdk.jar")
    into("$projectDir/libs")
}

dependencies {
    implementation files("libs/agora-rtm-sdk.jar") {
        builtBy "copyJarFromlib"
    }

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    api 'com.google.code.gson:gson:2.9.0'

    // rtc
    api 'io.agora.rtc:agora-special-full:4.1.1.13'
}